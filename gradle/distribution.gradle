/*
 * Copyright 2014 the original author or authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'distribution'

task install(dependsOn: "installDist")

distributions {
    main {
        contents {
            into('bin') {
                from { project(':gaiden-core').startScripts.outputs.files }
                fileMode = 0755
            }
            into('lib') {
                from project(':gaiden-core').configurations.runtime
                from project(':gaiden-core').jar
            }
            into('wrapper') {
                from { project(':gaiden-wrapper').startScripts.outputs.files }
                fileMode = 0755
            }
            into('wrapper/wrapper') {
                from project(':gaiden-wrapper').jar
            }
            into('wrapper/wrapper') {
                filter(ReplaceTokens, tokens: [version: version])
                from project(':gaiden-wrapper').sourceSets.main.resources.srcDirs
                include 'gaiden-wrapper.properties'
            }
            from files('README.md', 'NOTICE', 'LICENSE')
        }
    }
}
